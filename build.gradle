buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = "1.0.1-alpha"
group = "com.vitorxp.WorthClient"
archivesBaseName = "WorthClient"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
}

configurations {
    all {
        resolutionStrategy {
            force 'javax.vecmath:vecmath:1.5.2'
        }
        exclude group: 'javax.vecmath', module: 'vecmath'
    }
}

repositories {
    mavenCentral()
    flatDir { dirs 'libs' }
    maven {
        name = "forge"
        url = "https://files.minecraftforge.net/maven"
    }
    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/maven"
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }

    embed name: 'ViaVersion-5.3.0-downgraded'
    embed name: 'ViaBackwards-5.3.0-downgraded'
    embed name: 'ViaRewind-4.0.6-downgraded'
    embed 'org.yaml:snakeyaml:2.0'
    embed ("io.socket:socket.io-client:2.1.0")
}

mixin {
    defaultObfuscationEnv = "searge"
    add sourceSets.main, "mixins.worthclient.refmap.json"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
        attributes(
                "FMLCorePluginContainsFMLMod": "true",
                "ForceLoadAsMod": "true",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0,
                "MixinConfigs": "mixins.worthclient.json"
        )
    }
}

task fatJar(type: Jar) {
    dependsOn reobfJar
    classifier = 'release'
    from zipTree(jar.archivePath)
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/sponge_plugins.json"
        exclude "fabric.mod.json"
        exclude "plugin.yml", "bungee.yml", "velocity-plugin.json"
        exclude "META-INF/versions/**"
        exclude "**/module-info.class"
        exclude "**/LICENSE*", "**/NOTICE*"
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }

    manifest {
        attributes(
                "FMLCorePluginContainsFMLMod": "true",
                "ForceLoadAsMod": "true",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0,
                "MixinConfigs": "mixins.worthclient.json"
        )
    }
}

build.dependsOn fatJar